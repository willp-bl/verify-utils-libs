plugins {
      id "org.sonarqube" version "2.6.1"
}

apply plugin: 'idea'
apply plugin: 'java'


idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
    module {
        //if you love browsing Javadoc
        downloadJavadoc = true

        //and hate reading sources :)
        downloadSources = true
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    ext {
      build_version = "2.0.0-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
    }
    group = "uk.gov.ida"
    version = "$build_version"


    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/whitelisted-repos for production builds.\n\n')

            maven { url  "https://dl.bintray.com/alphagov/maven"      } // For dropwizard-logstash
            maven { url  "https://dl.bintray.com/alphagov/maven-test" } // For other public verify binaries

            jcenter()
        }
        else {
            maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
        }
    }


    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
        repositories {
            mavenLocal()
            maven {
                url "/srv/maven" // change to point to your repo, e.g. http://my.org/repo
            }
        }
    }
}

task sonar(dependsOn: [':clean', ':build', 'sonarqube'])

defaultTasks 'jar', 'publishToMavenLocal'

